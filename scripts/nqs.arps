#!/bin/csh
#QSUB -lM 20Mw
#QSUB -lT 3600 
#QSUB -s /bin/csh
#QSUB -eo
#QSUB -o nqs.arps.log 


#setenv NCPUS 8
#setenv NPROC 8

set EMAIL_ADRS = yuhe@tornado.gcn.uoknor.edu

set RCP_ADRS = yuhe@cumulus.gcn.uoknor.edu
set RCP_DIR = /scratch/cumulus/yuhe

set INPUT = arps.input
set rname = may20a
set subdir = arps4.0

set FAR_DIR = $subdir

#
# set working directory, copy files
#

cd $TMP

if ( ! -d $subdir ) then
  echo $TMP/$subdir does not exist.
  exit 1
endif

cd $TMP/$subdir
pwd

if ( ! -r $INPUT ) then
  echo 'The input file, $INPUT, does not exist'
  exit 1
endif

sed "\?runname?s/may20/$rname/g" $INPUT | cat > $rname.input

ja

makearps -p arps
if ( $status != 0 ) then
  echo Error -- makearps -p arps
  set success=failed
  goto fail_exit
else
  echo "Completed: arps_pvmt3d -npes 64 < input.name >! $rname.output"
  set success=successful
endif

arps < $rname.input >! $rname.output
if ( $status != 0 ) then
  echo Error -- arps < $rname.input >! $rname.output
  set success=failed
else
  echo "Completed: arps_pvmt3d -npes 64 < input.name >! $rname.output"
  set success=successful
endif

echo Finished ARPS run.

echo "FAR files to $FAR_DIR"

far mkdir $FAR_DIR
far mkdir $FAR_DIR/$rname

mv $rname.input  ..
mv $rname.maxmin ..
mv $rname.log    ..
mv $rname.output ..
mv $rname.rst*   ..
mv $rname.bin*   ..

cd ..

tar -cvf  $rname.tar $subdir 

far store $rname.tar    $FAR_DIR/$rname &
far store $rname.input  $FAR_DIR/$rname &
far store $rname.maxmin $FAR_DIR/$rname &
far store $rname.log    $FAR_DIR/$rname &
far store $rname.output $FAR_DIR/$rname &
far store $rname.bin*   $FAR_DIR/$rname &
far store $rname.rst*   $FAR_DIR/$rname &

echo Transfer data back to local disk

rcp -p $rname.input  $RCP_ADRS\:$RCP_DIR &
rcp -p $rname.log    $RCP_ADRS\:$RCP_DIR &
rcp -p $rname.maxmin $RCP_ADRS\:$RCP_DIR &
rcp -p $rname.output $RCP_ADRS\:$RCP_DIR &
rcp -p $rname.bin*   $RCP_ADRS\:$RCP_DIR &

wait

#
# The job is done. Send the messages out
#

fail_exit:

echo "Sending a message to $EMAIL_ADRS."

/usr/bin/mailx -s "Job $rname on MARIO is $success" $EMAIL_ADRS << END_mail

NQS job $rname on MARIO is $success. All source code, object code,
input, output, histroy data, restart data, and other related files
have been archived in the FAR space of MARIO.PSC.EDU in directory
$FAR_DIR/$rname.

Based on success, the following files have been transferred to

	$rname.input  to $RCP_ADRS\:$RCP_DIR
	$rname.maxmin to $RCP_ADRS\:$RCP_DIR
	$rname.log    to $RCP_ADRS\:$RCP_DIR
	$rname.output to $RCP_ADRS\:$RCP_DIR
	$rname.bin*   to $RCP_ADRS\:$RCP_DIR

END_mail

echo Run completed.

exit 0
